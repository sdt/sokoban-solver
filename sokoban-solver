#!/usr/bin/env perl
use 5.16.0;
use warnings;
use autodie;
use Function::Parameters qw(:strict);
use List::Util qw( max );
use Time::HiRes qw( usleep );
use Term::ANSIColor qw( colored );
use warnings FATAL => 'all';

# Sokoban solver
#
# Level files look like so: .
      ###
      #.#
  #####.#####
 ##         ##
##  # # # #  ##
#  ##     ##  #
# ##  # #  ## #
#     $@$     #
####  ###  ####
   #### ####

my $wall = '#';
my $hole = '.';
my $rock = '$';
my $dude = '@';
my $floor = ' ';

my $sleep_us = 100_000;
my $states = 0;

my $green = 'G';
my $red   = 'R';

# State is now an arrayref
#
# 0 = x
# 1 = y
# 2 = join(',', sort keys %rocks)
# 3 = %\rocks
# 4 = last move
# 5 = prev state
#
# State key now becomes join(',', @{$state}[0 .. 2])
#
# Most moves don't push rocks, and we spend a lot of time recomputing the
# state key.

my %moves = (
    U => [ 0, -1 ],
    D => [ 0, +1 ],
    L => [ -1, 0 ],
    R => [ +1, 0 ],
);

my %colors = (
    $dude  => 'magenta',
    $hole  => 'blue',
    $rock  => 'yellow',
    $red   => 'red',
    $green => 'cyan',
);

exit main(@ARGV);

fun main(@argv) {
    my ($board, $state) = parse_level($argv[0] // 'test.txt');
    my $solution = $argv[1] // solve_level($board, $state);
    if ($solution) {
        playback_solution($board, $state, $solution);
        say $solution;
    }
    else {
        say STDERR 'No solution';
    }
    say STDERR "$states states examined";
    return 0;
}

# Game configuration is:
# board + player(x,y) + @rocks[x,y] + @holes[x,y]
#
# Solution = @holes == 0 (or @rocks == 0)
#
# Move options:
#   @_  -> _@
#   @.  -> _@
#   @$_ -> _@$
#   @$. -> _@_
# Everything else means no move
#
# Another board config is:
#   image + player(x,y) + score
# Solution = score == 0
# Score drops on @$. -> _@_
#
# Another option: $object{$x:$y} = player | rock | wall | hole

fun solve_level($board, $state) {
    my @agenda = ( $state );

    my $seen_states = {};
    while (my $state = shift(@agenda)) {
        $states++;
        say STDERR "$states states, ", scalar @agenda,
                   " in queue, ", moves($state)
            if (($states & 0xFFFFF) == 0);
        for my $move (keys %moves) {
            my $new_state = try_move($board, $state, $move);
            next unless $new_state;
            my $state_rep = $new_state->[0]
                            . ',' . $new_state->[1]
                            . ',' . $new_state->[2];
            next if $seen_states->{$state_rep}++;
            $new_state->[4] = $move;
            $new_state->[5] = $state;
            return moves($new_state) if is_solution($board, $new_state);
            push(@agenda, $new_state);
        }
    }

    return;
}

fun moves($state) {
    my $moves = $state->[4];

    while (1) {
        $state = $state->[5];
        last unless $state->[4];
        $moves = $state->[4] . ',' . $moves;
    }
    $moves;
}

fun print_state($board, $state) {
    my ($px, $py, undef, $rocks) = @$state;
    for my $y (0 .. $board->{h}-1) {
        my $row;
        for my $x (0 .. $board->{w}-1) {
            my $c = ($x == $px && $y == $py) ?
                    $dude : lookup($board, $rocks, key($x, $y));
            $c = $floor if $c eq $red;
            $row .= colorise($c);
        }
        say STDERR $row;
    }
}

fun print_colormap($board, $state, $colormap) {
    my $rocks = $state->[3];
    for my $y (0 .. $board->{h}-1) {
        my $row;
        for my $x (0 .. $board->{w}-1) {
            my $key = key($x, $y);
            my $c = $rocks->{$key} // $colormap->{$key} // $floor;
            $row .= colorise($c);
        }
        say STDERR $row;
    }
}

fun colorise($char) {
    my $color = $colors{$char};
    return $color ? colored($char, $color) : $char;
}

fun playback_solution($board, $state, $solution) {
    print_state($board, $state);

    for my $move (split(/,/, $solution)) {
        $state = try_move($board, $state, $move);
        usleep($sleep_us);
        say '';
        print_state($board, $state);
    }
}

fun parse_level($filename) {
    open my $fh, '<', $filename;
    my $board = { };
    my $state = [ ];
    my $pos;
    my $y = 0;
    my $w = 0;
    for my $line (<$fh>) {
        my $x = 0;
        for my $char (split(//, $line)) {
            my $key = key($x, $y);
            if ($char eq $dude) {
                $state->[0] = $x;
                $state->[1] = $y;
            }
            elsif ($char eq $rock) { $state->[3]->{$key} = $char }
            elsif ($char eq $hole || $char eq $wall) { $board->{$key} = $char }
            $x++;
            $w = max($w, $x);
        }
        $y++;
    }
    $board->{w} = $w;
    $board->{h} = $y;
    my $holes = grep { $_ eq $hole } values %$board;
    my $rocks = keys %{ $state->[3] };
    die "$rocks rocks, but $holes holes" unless $holes == $rocks;
    die "No rocks or holes!" unless $holes;

    my $colormap = make_colormap($board);
    for my $key (keys %$colormap) {
        next unless $colormap->{$key} eq $red;
        $board->{$key} //= $red;
    }
    $state->[2] = rock_rep($state->[3]);
    $state->[4] = '';
    $state->[5] = undef;

    return ($board, $state);
}

fun rock_rep($rocks) {
    join(',', sort keys %$rocks);
}

fun is_solution($board, $state) {
    my $rocks = $state->[3];
    for my $key (keys %$rocks) {
        my $c = $board->{$key};
        return unless $c && $c eq $hole;
    }
    return 1;
}

fun try_move($board, $state, $move) {
    my ($x, $y, $rockrep, $rocks) = @$state;
    my ($dx, $dy) = @{ $moves{$move} };

    my $p0key = ($x + $dx) . ':' . ($y + $dy);
    my $p0 = $rocks->{$p0key} // $board->{$p0key} // $floor;
    if ($p0 eq $floor || $p0 eq $hole || $p0 eq $red) {
        # Just moving - no rock pushing
        return [ $x + $dx, $y + $dy, $rockrep, $rocks, $move, $state ];
    }
    elsif ($p0 eq $rock) {
        my $p1key = ($x + $dx * 2) . ':' . ($y + $dy * 2);
        my $p1 = $rocks->{$p1key} // $board->{$p1key} // $floor;
        if ($p1 eq $floor || $p1 eq $hole) { # $floor means not red!
            my %new_rocks = %$rocks;
            $new_rocks{$p1key} = $rock;
            delete $new_rocks{$p0key};
            return [ $x + $dx, $y + $dy, rock_rep(\%new_rocks), \%new_rocks,
                     $move, $state ];
        }
    }
    return;
}

fun lookup($board, $state, $key) {
    return $state->{$key} // $board->{$key} // $floor;
}

fun has_been_seen($state, $history) {
}

fun key($x, $y) { "$x:$y" }
fun unkey($key) { split(/:/, $key) }

fun make_colormap($board) {
    my $w = $board->{w};
    my $h = $board->{h};
    my $colormap = {};

    # First of all, holes are always green
    for my $key (grep { $board->{$_} eq $hole } keys %$board) {
        $colormap->{$key} = $green;
    }

    # And walls are red
    for my $key (grep { $board->{$_} eq $wall } keys %$board) {
        $colormap->{$key} = $red;
    }

    my $board_size = $w * $h;
    my $colors_filled = keys %$colormap;
    while (keys %$colormap < $board_size) {

        # For each green square, find any neighbouring squares that could be
        # pushed onto this green square, and colour them green also.
        for my $key (grep { $colormap->{$_} eq $green } keys %$colormap) {
            my ($x, $y) = unkey($key);
            for my $delta (values %moves) {
                my ($dx, $dy) = @$delta;
                my ($x0, $y0) = ($x + $dx, $y + $dy);
                my $key0 = key($x0, $y0);
                next if $colormap->{$key0};
                my ($x1, $y1) = ($x0 + $dx, $y0 + $dy);
                my $key1 = key($x1, $y1);
                next if $board->{$key1};
                $colormap->{$key0} = $green;
            }
        }
        my $prev = $colors_filled;
        $colors_filled = keys %$colormap;
        last if $prev == $colors_filled;
    }

    # Any remaining squares are now red
    for my $y (0 .. $h-1) {
        for my $x (0 .. $w-1) {
            $colormap->{key($x, $y)} //= $red;
        }
    }

    return $colormap;
}
